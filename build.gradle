import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.49")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '5.0.0'
    id 'org.sonarqube' version '2.7'
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'pmd'
    id 'info.solidsoft.pitest' version '1.3.0'
}

apply plugin: 'net.serenity-bdd.aggregator'

group = 'uk.gov.hmcts.reform.divorce'
version = '3.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs = ['src/integrationTest/java']
        }
        resources {
            srcDirs = ['src/integrationTest/resources']
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version
    }
    archiveName 'div-document-generator.jar'
}

mainClassName = 'uk.gov.hmcts.reform.divorce.documentgenerator.DocumentGeneratorApplication'

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    jcenter()

    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url 'https://repo.spring.io/libs-milestone' }
}

distributions {
    main {
        contents {
            from(file("$projectDir/lib/AI-Agent.xml")) {
                into "lib"
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

checkstyle {
    toolVersion = '8.21'
    configFile = project(':').file('checkstyle.xml')
    configProperties = ["suppressionFile": project(':').file('suppressions.xml')]
}

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
}

def versions = [
    commonsIo: '2.5',
    commonsLang3: '3.9',
    commonsBeanUtils: '1.9.4',
    feignHttpClient: '10.2.0',
    gradlePitest: '1.3.0',
    guava: '27.0.1-jre',
    jacksonDatabind: '2.10.0',
    jsonAssert: '1.2.3',
    junit: '4.12',
    lombok: '1.16.16',
    mockito: '2.8.9',
    nimbus: '5.1',
    pdfbox: '2.0.8',
    pitest: '1.3.2',
    powerMock: '1.7.3',
    puppyCrawl: '8.21',
    reformHealth: '0.0.3',
    reformPropertiesVolume: '0.0.4',
    reformsJavaLogging: '5.0.1',
    restAssured: '3.0.3',
    serenity: '2.0.41',
    serenityCucumber: '1.9.31',
    serviceTokenGenerator: '3.0.0',
    sonarPitest: '0.5',
    spring_security_rsa: '1.0.8.RELEASE',
    springBoot: '2.1.2.RELEASE',
    springCloud: '2.1.1.RELEASE',
    springfoxSwagger: '2.9.2',
    springHateoas: '0.25.1.RELEASE',
    unirest: '1.4.9',
    wiremockVersion: '2.19.0',
    reformSpringBootAutoConfigure: '1.1.0'
]

dependencies {
    //integration test
    integrationTestCompile group: 'com.mashape.unirest', name: 'unirest-java', version: versions.unirest
    integrationTestCompile group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: versions.nimbus
    integrationTestCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    integrationTestCompile group: 'junit', name: 'junit', version: versions.junit
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    integrationTestCompile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    integrationTestCompile group: 'org.apache.pdfbox', name: 'pdfbox', version: versions.pdfbox
    integrationTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestCompile group: 'org.skyscreamer', name:'jsonassert', version: versions.jsonAssert
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    integrationTestCompile (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator){
        exclude group: 'io.reactivex', module: 'io.reactivex'
        exclude group: 'io.reactivex', module: 'rxnetty'
        exclude group: 'io.reactivex', module: 'rxnetty-contexts'
        exclude group: 'io.reactivex', module: 'rxnetty-servo'
    }

    integrationTestCompile (group: 'com.google.guava', name:'guava', version: versions.guava) {
        force = true
    }
    compile (group: 'commons-beanutils', name: 'commons-beanutils', version: versions.commonsBeanUtils) {
        force = true
    }

    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-core', version: '9.0.22') {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-websocket', version: '9.0.22') {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-el', version: '9.0.22') {
        force = true
    }

    compile (group:  'org.springframework.security', name: 'spring-security-rsa', version: versions.spring_security_rsa) {
        force = true
    }

    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile (group: 'com.fasterxml.jackson.core', name:'jackson-databind', version: versions.jacksonDatabind) {
        force = true
    }

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    compile group: 'com.puppycrawl.tools', name: 'checkstyle', version:  versions.puppyCrawl
    compile group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
    compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: versions.reformSpringBootAutoConfigure

    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-ribbon', version: versions.springCloud
    compile (group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign',  version: versions.springCloud) {
        exclude group: 'io.reactivex', module: 'io.reactivex'
        exclude group: 'io.reactivex', module: 'rxnetty'
        exclude group: 'io.reactivex', module: 'rxnetty-contexts'
        exclude group: 'io.reactivex', module: 'rxnetty-servo'
        exclude group: 'org.bouncycastle'
    }

    compile (group: 'com.google.guava', name:'guava', version: versions.guava) {
        force = true
    }
    compile group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.feignHttpClient
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: versions.springHateoas
    compile group: 'org.apache.commons', name: 'commons-lang3', version : versions.commonsLang3

    compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: versions.reformHealth
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-appinsights', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: versions.reformPropertiesVolume
    compile (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator) {
        exclude group: 'io.reactivex', module: 'io.reactivex'
        exclude group: 'io.reactivex', module: 'rxnetty'
        exclude group: 'io.reactivex', module: 'rxnetty-contexts'
        exclude group: 'io.reactivex', module: 'rxnetty-servo'
        exclude group: 'org.bouncycastle'
    }

    compile group: 'org.pitest', name: 'pitest', version: versions.pitest
    compile group:'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    compile group:'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: versions.sonarPitest
    annotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testCompile group: 'com.github.tomakehurst', name:'wiremock', version: versions.wiremockVersion
    testCompile group: 'org.powermock', name:'powermock-api-mockito2', version:versions.powerMock
    testCompile group: 'org.powermock', name: 'powermock-api-mockito-common', version:versions.powerMock
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: versions.powerMock
    testCompile group: 'org.mockito', name: 'mockito-core', version: versions.mockito

    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
        exclude(module: 'commons-logging')
    }
    testCompile('org.springframework.security:spring-security-test')
    runtime('org.springframework.boot:spring-boot-devtools')

    runtime("org.springframework.boot:spring-boot-properties-migrator")
}

dependencyManagement {
    dependencies {
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
        dependencySet(
            group: 'com.fasterxml.jackson.core',
            version: '2.10.0'
        ) {
            entry 'jackson-core'
            entry 'jackson-databind'
        }
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/integrationTest/java')
        }
        resources {
            srcDir('src/integrationTest/resources')
        }
    }
}

dependencies {
    integrationTestCompile(sourceSets.test.output)
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    useJUnit {
        includeCategories 'uk.gov.hmcts.reform.divorce.documentgenerator.category.SmokeTest'
    }
}

task bootRunAat(type: BootRun, description: 'Runs the app using AAT config', dependsOn: 'classes', group: 'Application') {
    environment "http_proxy", "http://proxyout.reform.hmcts.net:8080"
    doFirst() {
        main = bootJar.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'aat'
    }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)

    finalizedBy aggregate
}

task printVersion {
    doLast {
        print project.version
    }
}

pmd {
    toolVersion = "6.11.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("ruleset.xml")
    ruleSets = []
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.divorce.documentgenerator.*']
    excludedClasses = ['uk.gov.hmcts.reform.divorce.documentgenerator.config.*',
                       'uk.gov.hmcts.reform.divorce.documentgenerator.domain.*',
                       'uk.gov.hmcts.reform.divorce.documentgenerator.exception.*']
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
}

def sonarExclusions = [
    '**uk/gov/hmcts/reform/divorce/documentgenerator/domain/**/*',
    '**uk/gov/hmcts/reform/divorce/documentgenerator/exception/*',
    '**uk/gov/hmcts/reform/divorce/documentgenerator/**/config/**/*',
    '**uk/gov/hmcts/reform/divorce/documentgenerator/DocumentGeneratorApplication.java'
]

sonarqube {
    properties {
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/apiTest.exec"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.projectKey", "DivorceDocumentGenerator"
        property "sonar.projectName", "Divorce :: document-generator"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

task developAddRelaseSuffix() {
    version = "${version}-SNAPSHOT"
}

jacocoTestReport {
    executionData(test, functional)

    reports {
        xml {
            enabled true
        }

        html {
            enabled true
        }
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

def debug = System.getProperty("debug")
run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5010']
    }
}
