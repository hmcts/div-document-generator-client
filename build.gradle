import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
    ext {
        springBootVersion = '2.7.18'
        springCloudBomVersion = "2021.0.8"
    }
    repositories {
        mavenCentral()
        maven { url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.5.8")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'com.github.ben-manes.versions' version '0.49.0'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '8.0.2'
    id 'org.sonarqube' version '6.2.0.5505'
    id 'org.springframework.boot' version '2.7.18'
    id 'info.solidsoft.pitest' version '1.9.11'
    id 'au.com.dius.pact' version '4.2.0'
    id 'uk.gov.hmcts.java' version '0.12.65'
}

apply plugin: 'checkstyle'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'org.owasp.dependencycheck'

group = 'uk.gov.hmcts.reform.divorce'
version = '3.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}

bootJar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
    archiveFileName = 'div-document-generator.jar'
}

springBoot {
    mainClass = 'uk.gov.hmcts.reform.divorce.documentgenerator.DocumentGeneratorApplication'
}

processTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

processIntegrationTestResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenCentral()
    maven { url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1' }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.register('fortifyScan', JavaExec) {
    mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
    ignoreFailures = true
    maxWarnings = 584
    toolVersion = '10.25.0'
    getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

def versions = [
    commonsIo: '2.7',
    commonsLang3: '3.9',
    commonsBeanUtils: '1.9.4',
    feignHttpClient: '10.2.0',
    gradlePitest: '1.4.5',
    guava: '33.4.8-jre',
    jacksonDatabind: '2.13.3',
    apacheLogging: '2.17.1',
    jsonAssert: '1.2.3',
    junit: '4.13.2',
    lombok: '1.18.30',
    logbackVersion: '1.2.10',
    nimbus: '5.14',
    pdfbox: '2.0.34',
    pitest: '1.4.10',
    powerMock: '2.0.9',
    puppyCrawl: '8.29',
    reformPropertiesVolume: '0.0.4',
    restAssured: '4.5.1',
    serenity: '2.6.0',
    serenityCucumber: '1.9.51',
    sonarPitest: '0.5',
    spring_security_rsa: '1.0.9.RELEASE',
    springHateoas: '0.25.1.RELEASE',
    unirest: '1.4.9',
    wiremockVersion: '2.27.2',
    springSecurityCrypto: '5.4.5',
    tomcatEmbedCore: '9.0.104',
    tomcatEmbedWebsocket: '9.0.104',
    tomcatEmbedEl: '9.0.106',
    pact_version: '4.1.11',
    httpComponents: '4.5.14',
    bouncycastle: '1.61',
    hmctsLogging: '6.1.9'
]

dependencies {
    //integration test
    integrationTestImplementation group: 'com.mashape.unirest', name: 'unirest-java', version: versions.unirest
    integrationTestImplementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: versions.nimbus
    integrationTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity, {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    integrationTestImplementation group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    integrationTestImplementation group: 'org.apache.pdfbox', name: 'pdfbox', version: versions.pdfbox
    integrationTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestImplementation group: 'org.skyscreamer', name:'jsonassert', version: versions.jsonAssert
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    integrationTestImplementation 'org.springframework.boot:spring-boot-starter-web'
    integrationTestImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.2'
    integrationTestImplementation group: 'io.github.openfeign', name: 'feign-jackson', version: versions.feignHttpClient

    integrationTestImplementation (group: 'com.google.guava', name:'guava', version: versions.guava)
    implementation (group: 'commons-beanutils', name: 'commons-beanutils', version: versions.commonsBeanUtils)

    implementation (group: 'org.apache.tomcat.embed', name:'tomcat-embed-core', version: versions.tomcatEmbedCore)
    implementation (group: 'org.apache.tomcat.embed', name:'tomcat-embed-websocket', version: versions.tomcatEmbedWebsocket)
    implementation (group: 'org.apache.tomcat.embed', name:'tomcat-embed-el', version: versions.tomcatEmbedEl)

    implementation (group:  'org.springframework.security', name: 'spring-security-rsa', version: versions.spring_security_rsa) {
        exclude group: 'org.springframework.security', module: 'spring-security-crypto'
    }

    implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: versions.springSecurityCrypto

    implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.9'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: versions.bouncycastle

    implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: versions.apacheLogging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.apacheLogging

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.33'

    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'

    implementation (group: 'com.google.guava', name:'guava', version: versions.guava)
    implementation group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.feignHttpClient
    implementation group: 'org.springframework.hateoas', name: 'spring-hateoas', version: versions.springHateoas
    implementation group: 'org.apache.commons', name: 'commons-lang3', version : versions.commonsLang3

    implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: versions.hmctsLogging

    implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.2'

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logbackVersion
    implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logbackVersion
    implementation group: 'junit', name: 'junit', version: versions.junit
    implementation group: 'org.pitest', name: 'pitest', version: versions.pitest
    implementation group:'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    implementation group:'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: versions.sonarPitest
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.httpComponents

    annotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    implementation "org.springframework.boot:spring-boot-starter-validation"
    testImplementation group: 'com.github.tomakehurst', name:'wiremock', version: versions.wiremockVersion
    testImplementation('org.mockito:mockito-core:2.23.4')
    testImplementation group: 'org.powermock', name:'powermock-api-mockito2', version:versions.powerMock
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: versions.powerMock
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.mockito', module: 'mockito-core'
        exclude(module: 'commons-logging')
    }
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.9', classifier: 'all'
    runtimeOnly('org.springframework.boot:spring-boot-devtools')

    runtimeOnly("org.springframework.boot:spring-boot-properties-migrator")

    contractTestImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.httpComponents
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version

    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    contractTestImplementation("org.junit.jupiter:junit-jupiter-api:5.7.2")
    contractTestRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.2")

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudBomVersion}"
    }
    dependencies {
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'
        dependencySet(group: 'com.fasterxml.jackson.core', version: versions.jacksonDatabind) {
            entry 'jackson-core'
            entry 'jackson-databind'
            entry 'jackson-annotations'
        }

        dependencySet(group: 'org.apache.logging.log4j', version: '2.19.0') {
            entry 'log4j-api'
            entry 'log4j-to-slf4j'
        }

        dependencySet(group: 'org.springframework', version: '5.3.39') {
            entry 'spring-aop'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-context-support'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-web'
            entry 'spring-webmvc'
        }

        //CVE-2022-25857
        dependencySet(group: 'org.yaml', version: '1.33') {
            entry 'snakeyaml'
        }

        dependency group: 'com.netflix.servo',name: 'servo-core', version: '0.13.2'
        dependency group: 'org.glassfish', name: 'jakarta.el', version: '3.0.4'
    }
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }

    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

dependencies {
    integrationTestImplementation(sourceSets.test.output)
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
}

processContractTestResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processIntegrationTestResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('smoke', Test) {
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)

    useJUnit {
        includeCategories 'uk.gov.hmcts.reform.divorce.documentgenerator.category.SmokeTest'
    }
}

tasks.register('bootRunAat', BootRun) {
    dependsOn 'classes'
    main = bootJar.mainClass
    doFirst() {
        classpath = sourceSets.main.runtimeClasspath
        systemProperties = System.properties
        systemProperty 'spring.profiles.active', 'aat'
    }
}

tasks.register('functional', Test) {
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)

    //finalizedBy aggregate
    jvmArgs += [
        "--add-opens=java.base/java.lang=ALL-UNNAMED",
        "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED"
    ]
}

tasks.register('contract', Test) {
    group = 'Delivery pipeline'
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
}


tasks.register('printVersion') {
    doLast {
        print project.version
    }
}

test {
    jvmArgs += ['--add-opens', 'java.base/java.lang=ALL-UNNAMED']
    jvmArgs += ['--add-opens', 'java.base/java.util=ALL-UNNAMED']
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.divorce.documentgenerator.*']
    excludedClasses = ['uk.gov.hmcts.reform.divorce.documentgenerator.config.*',
                       'uk.gov.hmcts.reform.divorce.documentgenerator.domain.*',
                       'uk.gov.hmcts.reform.divorce.documentgenerator.exception.*']
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
}

tasks.register('developAddRelaseSuffix') {
    version = "${version}-SNAPSHOT"
}

jacocoTestReport {
    executionData(test)

    reports {
        xml.required = true
        html.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

sonarqube {
    def sonarExclusions = [
        '**uk/gov/hmcts/reform/divorce/documentgenerator/domain/**/*',
        '**uk/gov/hmcts/reform/divorce/documentgenerator/exception/*',
        '**uk/gov/hmcts/reform/divorce/documentgenerator/**/config/**/*',
        '**uk/gov/hmcts/reform/divorce/documentgenerator/DocumentGeneratorApplication.java'
    ]
    properties {
        property "sonar.projectKey", "DivorceDocumentGenerator"
        property "sonar.projectName", "Divorce :: document-generator"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.dynamicAnalysis", "reuseReports"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.outputLocation.get()}"
        property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
    }
}

def debug = System.getProperty("debug")
run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5010']
    }
}

project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}

tasks.register('runAndPublishConsumerPactTests', Test) {
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish

static def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
    publish {
        pactDirectory = 'pacts'
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
        tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
        version = project.pactVersion
    }
}
